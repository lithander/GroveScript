/*
This example demonstrates the use of conditions and attached commands in production rules. 

#ResolveFoo is responsible for generating the commands that are drawing the colored extensions.
There are three rules that are applied in alternation. Each rule has a condtion that requireds a counter variable 'i' to be of a specific value. When the rule is rule is applied the attachment is executed and raises raises 'i' or in case of the blue variant resets it to 0.
*/

position 0, 120
set i, 0
set t, time()*180
set step, 1.5
set size, 2
size size

seed Fractal, Right
repeat 2
	grow Fractal, RightRule
	grow Fractal, LeftRule

grow Fractal, ResolveFoo
	
run Fractal
rotate 180
run Fractal

#Right
move step
rotate 90
move step

#Left
move step
rotate -90
move step

#Red
rgb 1,0,0
size size*3
#Green
rgb 0,1,0
size size*3
#Blue
rgb 0,0,1
size size*3

#Base
rotate 90
move step*sin(t)
rotate 180
size size
rgb 1,1,1
move step*sin(t)
rotate 90

#ResolveFoo
Foo : i = 0 -> Red Base
	raise i
Foo : i = 1 -> Green Base
	raise i
Foo -> Blue Base
	set i, 0
	
#RightRule
Right -> Right Left Foo Left Right

#LeftRule
Left -> Left Right Foo Right Left
